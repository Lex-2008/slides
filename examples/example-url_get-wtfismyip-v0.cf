#!/var/cfengine/bin/cf-agent -f-
body file control
{
      inputs => { '$(sys.libdir)/stdlib.cf' };
}

bundle agent example_url_get_wtfismyip_v0
{
  vars:
      # This is the URL that we will query.
      "url" string => "https://wtfismyip.com/json";

      # We don't need to supply any specific options in this case.
      # url.max_content is a typical setting to change. 4K isn't enough for
      # some, (many?) API responses.
      "options" data => '{}';

      # Here we make our http(s) call, storing the response in `d`
      "d" data => url_get( $(url), @(options));

      # The response content is returned as a *string* inside the `content`. In
      # order to work with it, we need to pull it into a proper data container.
      "content" data => "$(d[content])";

  reports:

      # Let's view the multi-line JSON representation of the `url_get()` response.
      "$(with)" with => string_mustache( "{{%-top-}}", d);

      # Let's view the multi-line JSON representation of our extracted `content`
      # datacontainer.
      "$(with)" with => string_mustache( "{{%-top-}}", content);
}
bundle agent __main__{methods:"example_url_get_wtfismyip_v0";}
